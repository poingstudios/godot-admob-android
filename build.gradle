// Top-level build file where you can add configuration options common to all sub-projects/modules.
plugins {
    id 'com.android.application' version '8.13.2' apply false
    id 'com.android.library' version '8.13.2' apply false
    id 'org.jetbrains.kotlin.android' version '2.3.0' apply false
}

allprojects {
    afterEvaluate {

    }
}

ext {
    pluginExportPath = project.hasProperty('pluginExportPath') ? project.property('pluginExportPath') : null
    aarExportPath = project.hasProperty('pluginExportPath') ? project.property('pluginExportPath') + '\\libs'  : null
}

tasks.register('clean', Delete) {
    delete rootProject.layout.buildDirectory
}

tasks.register('exportAar', Copy) {
    def aarFiles = []
    subprojects { subproject ->
        if (subproject.file("src").exists() && subproject.name != "core") {
            def moduleAarDir = subproject.file("build/outputs/aar")
            aarFiles.addAll(fileTree(moduleAarDir) {
                include '*.aar'
            })
        }
    }
    from aarFiles
    into aarExportPath
}

tasks.register('exportGdap', Copy) {
    def gdapFiles = []
    subprojects { subproject ->
        if (subproject.file("src").exists()) {
            def configGdapDir = subproject.file("config")
            gdapFiles.addAll(fileTree(configGdapDir) {
                include '*.gd'
            })
        }
    }
    from gdapFiles
    into pluginExportPath
}


tasks.register('exportFiles') {
    dependsOn exportAar, exportGdap
}
gradle.taskGraph.whenReady { taskGraph ->
    if (taskGraph.hasTask(exportFiles)) {
        if (pluginExportPath == null) {
            throw new GradleException("The export path for AAR files is not defined. Use the command './gradlew exportFiles -PpluginExportPath=<desired-path>' to export the AAR files.")
        }
        println "Exporting AAR files to: $pluginExportPath"
    }
}

tasks.register('zipPlugins', Zip) {
    def godotVersion = project.hasProperty("godotVersion") ? project.getProperty("godotVersion") : ""
    def zipFileName = "poing-godot-admob-android${godotVersion ? '-v' + godotVersion : ''}.zip"

    archiveFileName.set(zipFileName)
    destinationDirectory.set(file("${rootProject.projectDir}/.output"))

    subprojects { subproject ->
        if (subproject.file("src").exists() && subproject.name != "core") {
            def moduleAarDir = subproject.file("build/outputs/aar")
            def aarFiles = fileTree(moduleAarDir) {
                include '*release.aar'
            }

            def configGdapDir = subproject.file("config")
            def gdapFiles = fileTree(configGdapDir) {
                include '*.gdap'
            }

            into("${subproject.name}") {
                from(gdapFiles) {
                    include '*.gdap'
                }
                into("libs") {
                    from(aarFiles)
                }
            }
        }
    }
}