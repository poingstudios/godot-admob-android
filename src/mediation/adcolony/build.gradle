plugins {
    id 'com.android.library'
    id 'org.jetbrains.kotlin.android'
}

android {
    namespace = "com.poingstudios.godot.admob.adcolony"

    defaultConfig {
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles "consumer-rules.pro"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    def startName = "poing-godot-admob"
    def moduleName = project.getName().replaceAll(":", "-") //the module that is being built at the moment
    def fullModuleName = "$startName-$moduleName"

    println "Module find: module $moduleName"

    libraryVariants.configureEach { variant ->
        variant.outputs.all {
            def outputName = "${fullModuleName}-${variant.name}.aar"
            println "Output file will be: $outputName"
            outputFileName = "$outputName"
        }
    }
}

dependencies {
    compileOnly project(path: ':src:core')

    File gd_file = file("config/poing_godot_admob_adcolony.gd")
    def match = gd_file.text =~ /(?<=_dependency_library := )\[[^\]]+\]/

    def remote_content = Eval.me(match[0])

    remote_content.each { item ->
        println "compileOnly item: $item"
        project.dependencies.add("compileOnly", item.toString())
    }

    implementation 'androidx.appcompat:appcompat:1.7.1'
    implementation 'com.google.android.material:material:1.13.0'
    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.3.0'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.7.0'
}