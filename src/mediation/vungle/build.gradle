plugins {
    id 'com.android.library'
    id 'org.jetbrains.kotlin.android'
}

android {
    compileSdk 33

    defaultConfig {
        minSdk 24
        targetSdk 33

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles "consumer-rules.pro"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }


    def configFile = file('config/poing-godot-admob-vungle.gdap')

    def startName = "poing-godot-admob"
    def moduleName = project.getName().replaceAll(":", "-") //the module that is being built at the moment
    def fullModuleName = "$startName-$moduleName"
    def versionRegex = /${fullModuleName}-(.+?)-(?:release|debug).aar"/

    def versionMatch = configFile.text =~ versionRegex
    def version = versionMatch[0][1] //if the version is not found, there will be an error, which should be expected because the generated version must be the same as the one in the configuration file
    println "Version find: $version, of module $moduleName"

    libraryVariants.all { variant ->
        variant.outputs.all {
            def outputName = "${fullModuleName}-${version}-${variant.name}.aar"
            println "Output file will be: $outputName"
            outputFileName = "$outputName"
        }
    }
}

dependencies {
    compileOnly project(path: ':src:core')
    File gdap_file = file("config/poing-godot-admob-vungle.gdap")
    def match = gdap_file.text =~ /(?<=remote=)[^;]+/

    def remote_content = Eval.me(match[0])

    for(item in remote_content){
        println "compileOnly item: $item"
        compileOnly item
    }

    implementation 'androidx.appcompat:appcompat:1.6.1'
    implementation 'com.google.android.material:material:1.9.0'
    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.5'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.5.1'
}