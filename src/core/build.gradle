plugins {
    id 'com.android.library'
    id 'org.jetbrains.kotlin.android'
}

android {
    namespace "com.poingstudios.godot.admob.core"
    compileSdk 35

    defaultConfig {
        minSdk 29
        targetSdk 35

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles "consumer-rules.pro"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    def startName = "poing-godot-admob"
    def moduleName = project.getName().replaceAll(":", "-") //the module that is being built at the moment
    def fullModuleName = "$startName-$moduleName"

    def versionMatch = configFile.text =~ versionRegex
    def version = versionMatch[0][1] //if the version is not found, there will be an error, which should be expected because the generated version must be the same as the one in the configuration file
    ndkVersion '28.1.13356709'
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }
    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_17
    }
    println "Module find: module $moduleName"

    libraryVariants.all { variant ->
        variant.outputs.all {
            def outputName = "${fullModuleName}-${variant.name}.aar"
            println "Output file will be: $outputName"
            outputFileName = "$outputName"
        }
    }
}
def isCi = System.getenv("CI") != null || project.hasProperty("ci")

dependencies {
    File gdap_file = file("../ads/config/poing-godot-admob-ads.gdap")
    def match = gdap_file.text =~ /(?<=remote=)[^;]+/

    def remote_content = Eval.me(match[0])

    for(item in remote_content){
        println "api item: $item"
        api item
    }

    if (isCi) {
        println "Using local godot-lib"

        api project(':libs:godot-lib')
    } else {
        def godotVersion = "4.5.1"
        def releaseType = "stable"
        def fullVersion = "$godotVersion.$releaseType"
        println "Using remote godot-lib on version $fullVersion"

        api "org.godotengine:godot:$fullVersion"
    }

    implementation 'androidx.appcompat:appcompat:1.6.1'
    implementation 'com.google.android.material:material:1.9.0'
    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.5'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.5.1'
}

task copyAar(type: Copy) {
    dependsOn 'assemble'

    from(layout.buildDirectory.dir("outputs/aar"))
    include '*.aar'

    into project(':src:ads').layout.buildDirectory.dir("outputs/aar")
}

tasks.build.dependsOn copyAar