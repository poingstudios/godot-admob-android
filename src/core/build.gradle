plugins {
    id 'com.android.library'
    id 'org.jetbrains.kotlin.android'
}

android {
    namespace = "com.poingstudios.godot.admob.core"

    defaultConfig {
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles "consumer-rules.pro"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    def startName = "poing-godot-admob"
    def moduleName = project.getName().replaceAll(":", "-") //the module that is being built at the moment
    def fullModuleName = "$startName-$moduleName"

    println "Module find: module $moduleName"

    libraryVariants.configureEach { variant ->
        variant.outputs.all {
            def outputName = "${fullModuleName}-${variant.name}.aar"
            println "Output file will be: $outputName"
            outputFileName = "$outputName"
        }
    }
}

dependencies {
    def libs = rootProject.ext.readDependencyLibrary(
            file("../ads/config/poing_godot_admob_ads.gd")
    )

    libs.each { item ->
        println "api item: $item"
        project.dependencies.add("api", item.toString())
    }

    def godotVersion    = project.findProperty("godotVersion") ?: "4.5.1"
    def releaseType     = project.findProperty("releaseType") ?: "stable"

    def fullVersion = "$godotVersion.$releaseType"
    println "Using remote godot-lib on version $fullVersion"

    api "org.godotengine:godot:$fullVersion"
    implementation 'androidx.appcompat:appcompat:1.7.1'
    implementation 'com.google.android.material:material:1.13.0'
    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.3.0'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.7.0'
}

tasks.register('copyAar', Copy) {
    dependsOn 'assemble'

    from(layout.buildDirectory.dir("outputs/aar"))
    include '*.aar'

    into project(':src:ads').layout.buildDirectory.dir("outputs/aar")
}

tasks.build.dependsOn copyAar
